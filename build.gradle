plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

group 'net.valneas'
version '0.4.0'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    apiElements 'org.mongodb:mongodb-driver:3.12.11'
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    ext {
        pluginName = ''
    }

    afterEvaluate {
        shadowJar {
            archivesBaseName = pluginName
        }

        jar {
            manifest {
                attributes 'pluginName': pluginName
            }
        }

        publishing {
            publications {
                shadow(MavenPublication) { publication ->
                    groupId = 'io.github.azodox'
                    artifactId = pluginName
                    version = version /*+ '-SNAPSHOT'*/
                    project.shadow.component(publication)

                    pom {
                        description = 'My original way to manage accounts on a minecraft server.'
                        url = 'https://github.com/Azodox/account-system'

                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'http://www.opensource.org/licenses/mit-license.php'
                            }
                        }
                        developers {
                            developer {
                                id = 'Azodox_'
                                name = 'Azodox_'
                            }
                        }
                        scm {
                            connection = 'https://github.com/Azodox/account-system.git'
                            developerConnection = 'https://github.com/Azodox/account-system.git'
                            url = 'https://github.com/Azodox/account-system'
                        }
                    }
                }
            }

            repositories {
                maven {
                    url 'https://s01.oss.sonatype.org/content/repositories/releases/'

                    credentials {
                        username = findProperty("centralUser")
                        password = findProperty("centralPassword")
                    }
                }
            }
        }

        signing {
            sign publishing.publications.shadow
        }
    }
}