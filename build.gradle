import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
}

group 'net.valneas'
version '0.3.3-xmas'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = 17

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://repo.codemc.org/repository/maven-public/"
    }
    maven { url "https://papermc.io/repo/repository/maven-public/" }
}

dependencies {
    shadow "io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT"
    implementation "io.github.llewvallis:command-builder:2.2.0"
    implementation "io.github.leonardosnt:bungeechannelapi:1.0.0-SNAPSHOT"
    implementation 'org.mongodb:mongodb-driver:3.12.10'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
    duplicatesStrategy = 'include'
}
